buildscript {
    repositories {
         jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

plugins {
    id 'org.sonarqube' version '2.6'
    id 'com.github.jk1.dependency-license-report' version '0.3.14'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'jacoco'

project.ext {
    log4jVersion = "2.8"
    elasticsearchVersion = "5.5.0"
    personIndexerJob = "gov.ca.cwds.jobs.PersonIndexerJob"
    dropwizardVersion = "1.1.0"
}

version = 0.35

repositories {
    mavenCentral()
    maven {
        url "http://maven2.cwds.tabordasolutions.net"
    }
    maven {
        url "https://artifacts.elastic.co/maven"
    }
}

dependencies {
    // ES 5.x:
    compile group: 'org.elasticsearch', name: 'elasticsearch', version: elasticsearchVersion
    compile group: 'org.elasticsearch.client', name: 'transport', version: elasticsearchVersion
    compile ("org.elasticsearch.client:x-pack-transport:" + elasticsearchVersion)
    
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
	compile group: 'javax.validation', name: 'validation-api', version: '1.1.0.Final'

	// LOGGING:	
	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
  	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
	compile group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4jVersion
	compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.1'
	compile group: 'org.slf4j', name: 'slf4j-ext', version: '1.7.25'

	compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
	compile 'com.google.inject.extensions:guice-jmx:4.1.0'
	
	compile group: "org.postgresql", name: "postgresql", version: "9.4.1212"
	compile group: "io.dropwizard", name: "dropwizard-hibernate", version: "1.0.0-rc4"
	compile 'org.hibernate:hibernate-c3p0:5.2.10.Final'

	// Exclude transitive dependencies.
	compile ('gov.ca.cwds.api:api-core:3.6.493') {
	    exclude group: 'org.glassfish.jersey.core'
        exclude group: 'ch.qos.logback'
        exclude group: 'org.slf4j'
	}

    compile group: "org.glassfish.jersey.ext", name: "jersey-declarative-linking", version: "2.23.2"
	compile group: "javax.el", name: "javax.el-api", version: "2.2.4"
	compile group: "org.glassfish.web", name: "javax.el", version: "2.2.4"
	compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.8.3'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'

	compile 'com.fasterxml.jackson.module:jackson-module-guice:2.8.2'
    compile group: 'org.json', name: 'json', version: '20090211'
	compile 'io.reactivex.rxjava2:rxjavafx:2.2.0'
	compile group: 'org.weakref', name: 'jmxutils', version: '1.19'
	compile 'org.quartz-scheduler:quartz:2.3.0'
	compile 'com.google.code.gson:gson:2.8.2'
	
	compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'

	// Maven certificate problem.
	// compile 'io.earcam:io.earcam.unexceptional:0.3.0'

    compile fileTree(dir: 'lib', include: ['*.jar'])

    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.195'
	testCompile group: 'org.mockito', name: 'mockito-core', version: '2.10.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'
    testCompile('org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.23.2') {
        exclude group: 'javax.servlet', module: 'javax.servlet-api'
        exclude group: 'junit', module: 'junit'
    }

	testCompile group: "io.dropwizard", name: "dropwizard-testing", version: dropwizardVersion
    testCompile group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '2.1.6'
    testCompile 'org.ow2.asm:asm-all:5.2'
	testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
	testCompile 'io.exemplary.guice:guice-behave:1.0.0'
	
	testCompile 'com.github.tlrx:elasticsearch-test:1.2.1'
	// testCompile group: 'org.elasticsearch.test', name: 'framework', version: 'elasticsearchVersion'
    // testCompile 'org.jacoco:jacoco:0.7.9'
}

shadowJar {
  classifier = ''
  baseName = (System.properties.getProperty('baseName') != null) ? System.properties.getProperty('baseName') : 'DocumentIndexerJob'
  mergeServiceFiles()	
  manifest {
    attributes 'Main-Class': (System.properties.getProperty('mainClass') != null) ? System.properties.getProperty('mainClass') : 'gov.ca.cwds.jobs.DocumentIndexerJob'
  }
  version version + ((System.properties.getProperty('build') != null ) ? '.' + System.properties.getProperty('build') : '')
}

publishing {
  publications {
    shadow(MavenPublication) {
      from components.shadow
      groupId 'gov.ca.cwds.jobs'
      artifactId 'jobs'
        version version + ((System.properties.getProperty('build') != null ) ? '.' + System.properties.getProperty('build') : '')
    }
  }
  repositories {
    maven {
      // OPTION: set to http url.
      url "file:///usr/share/nginx/html/maven2"
    }
  }
}

task printProps << {
    println System.properties['DB_CMS_JDBC_URL']
}

tasks.withType(Test) {
    enableAssertions = true
    systemProperties project.properties.subMap(["DB_CMS_JDBC_URL", "DB_CMS_PASSWORD", "DB_CMS_SCHEMA", "DB_CMS_REP_SCHEMA", "DB_CMS_USER", "DB_NS_JDBC_URL", "DB_NS_PASSWORD", "DB_NS_SCHEMA", "DB_NS_USER"])
}

task libLicenceFolders(type: Copy){
    from 'build/reports/dependency-license'
    into 'legal/licenses'
    exclude "licenses.html"
    exclude "licenses.txt"
}

task libLicenseReportFiles(type: Copy){
    from 'build/reports/dependency-license'
    into 'legal'
    include "licenses.html"
    include "licenses.txt"
}

task libLicenseReport(dependsOn:[generateLicenseReport, libLicenceFolders,
                                 libLicenseReportFiles ] ){
}

jacocoTestReport {
  reports {
      xml.enabled false
      csv.enabled false
      html.destination "${buildDir}/jacocoHtml"
  }
}

sonarqube {
  properties {
      property "sonar.jacoco.reportPath",   "${buildDir}/jacoco/test.exec"
      property "sonar.jacoco.itReportPath", "${buildDir}/jacoco/integrationTest.exec"     
  }
}

licenseReport {
    renderer = new CWDSReportRenderer()
}

//==================================================================================================
// Custom Groovy classes for Licence Reports.
// Move to own classes outside of build.
import org.gradle.api.Project;
import com.github.jk1.license.License;
import com.github.jk1.license.LicenseReportPlugin;
import com.github.jk1.license.ManifestData;
import com.github.jk1.license.ModuleData;
import com.github.jk1.license.PomData;
import com.github.jk1.license.ProjectData;
import com.github.jk1.license.render.ReportRenderer;

public class CWDSReportRenderer implements ReportRenderer {

    @Override
    void render(ProjectData data) {
        new CWDSTextReportRenderer().render(data)
        new CWDSHtmlReportRenderer().render(data)

    }
}
public class CWDSTextReportRenderer implements ReportRenderer {

  private Project project
  private LicenseReportPlugin.LicenseReportExtension config
  private File output
  private int counter
  private String fileName

  CWDSTextReportRenderer(String filename = 'licenses.txt'){
      this.fileName = filename
  }
  public void render(ProjectData data) {
        project = data.project
        config = project.licenseReport
        output = new File(config.outputDir, fileName)
        output.text = """
================================================================
       Dependency License Report for $project.name ${if (!'unspecified'.equals(project.version))
            project.version else ''}
================================================================
"""
      output << "\n\n"
        printDependencies(data)
        output << """
This report was generated at ${new Date()}.
"""
    }

    private void printDependencies(ProjectData data) {
        data.allDependencies.sort().each {
            printDependency(it)
        }
    }

    private String printDependency(ModuleData data) {
        boolean projectUrlDone = false
        if (data.name) output << "License Name: $data.name "
        if (data.version) output << " Version: $data.version\n\n"

        if (data.poms.isEmpty() && data.manifests.isEmpty()) {
            output << "No license information found\n\n"
            return
        }

        if (!data.manifests.isEmpty() && !data.poms.isEmpty()) {
            ManifestData manifest = data.manifests.first()
            PomData pomData = data.poms.first()
            if (manifest.url && pomData.projectUrl && manifest.url == pomData.projectUrl) {
                output << "Project URL: $manifest.url\n\n"
                projectUrlDone = true
            }
        }

        if (!data.manifests.isEmpty()) {
            ManifestData manifest = data.manifests.first()
            if (manifest.url && !projectUrlDone) {
                output << "Manifest Project URL: $manifest.url\n\n"
            }
            if (manifest.license) {
                if (manifest.license.startsWith("http")) {
                    output << "Manifest license URL: $manifest.license\n\n"
                } else if (manifest.hasPackagedLicense) {
                    output << "Packaged License File: $manifest.license\n\n"
                } else {
                    output << "Manifest License: $manifest.license (Not packaged)\n\n"
                }
            }
        }

        if (!data.poms.isEmpty()) {
            PomData pomData = data.poms.first()
            if (pomData.projectUrl && !projectUrlDone) {
                output << "POM Project URL: $pomData.projectUrl\n\n"
            }
            if (pomData.licenses) {
                pomData.licenses.each { License license ->
                    output << "POM License: $license.name"
                    if (license.url) {
                        if (license.url.startsWith("http")) {
                            output << " - $license.url\n\n"
                        } else {
                            output << "License: $license.url\n\n"
                        }
                    }
                }
            }
        }
        if (!data.licenseFiles.isEmpty() && !data.licenseFiles.first().files.isEmpty()) {
            output << 'Embedded license: '
            output << "\n\n"
            output << data.licenseFiles.first().files.collect({ "                    " +
                    "****************************************\n\n \t\t\t$it\n\n" + new
                    File("$config.outputDir/$it").text + "\n"}).join('')
        }
        output <<
                "\n\n================================================================================\n\n"
    }
}

import com.github.jk1.license.LicenseReportPlugin.LicenseReportExtension
class CWDSHtmlReportRenderer implements ReportRenderer {

    private Project project
    private LicenseReportExtension config
    private File output
    private int counter
    private String fileName
    private String libLicenseUrl

    CWDSHtmlReportRenderer(String libLicenseUrl = "licenses",String fileName = 'licenses.html') {
        this.fileName = fileName
        this.libLicenseUrl = libLicenseUrl
    }

    public void render(ProjectData data) {
        project = data.project
        config = project.licenseReport
        output = new File(config.outputDir, fileName)
        output.text = """
<html>
<head>
<title>Dependency License Report for $project.name</title>
<style>
    table, th, td { border: 1px solid black; }
</style>
<head>
<body>
<h1>Dependency License Report for $project.name ${if (!'unspecified'.equals(project.version)) project.version else ''}</h1>
"""
        printDependencies(data)
        output << """
<hr />
<p id="timestamp">This report was generated at <em>${new Date()}</em>.</p>
</body>
</html>
"""
    }

    private void printDependencies(ProjectData data) {
        output << "<table >"
        output << "<hr><th>name</th><th>project url</th><th>license</th>" +
                       "<th>Embeded License</th></hr>"
        data.allDependencies.sort().each {
            output << "<tr>"
            printDependency(it)
            output << "</tr>"
        }
        output << "</table>"
    }

    private String printDependency(ModuleData data) {
        boolean projectUrlDone = false
        String name = ""
        String urls = ""
        String licenses = ""
        name += "<p><strong> ${++counter}.</strong> "
        if (data.name) name += "$data.name "
        name += "</p>"

        if (data.poms.isEmpty() && data.manifests.isEmpty()) {
            name += "<p><strong>No license information found</strong></p>"
        }

        if (!data.manifests.isEmpty() && !data.poms.isEmpty()) {
            ManifestData manifest = data.manifests.first()
            PomData pomData = data.poms.first()
            if (manifest.url && pomData.projectUrl && manifest.url == pomData.projectUrl) {
                urls += "<p> <code><a href=\"$manifest.url\">$manifest.url</a></code></p>"
                projectUrlDone = true
            }
        }

        if (!data.manifests.isEmpty()) {
            ManifestData manifest = data.manifests.first()
            if (manifest.url && !projectUrlDone) {
                urls += "<p> <code><a href=\"$manifest.url\">$manifest.url</a></code></p>"
            }
            if (manifest.license) {
                if (manifest.license.startsWith("http")) {
                    licenses += "<p><strong>Manifest license URL:</strong> <a href=\"$manifest.license\">$manifest.license</a></p>"
                } else if (manifest.hasPackagedLicense) {
                    licenses += "<p><strong>Manifest license URL:</strong> <a href=\"$manifest.url\">$manifest.license</a></p>"
                } else {
                    licenses += "<p><strong>Manifest license URL:</strong>$manifest.license (Not packaged)</p>"
                }
            }
        }

        if (!data.poms.isEmpty()) {
            PomData pomData = data.poms.first()
            if (pomData.projectUrl && !projectUrlDone) {
                urls += "<p> <code><a href=\"$pomData.projectUrl\">$pomData.projectUrl</a></code></p>"
            }
            if (pomData.licenses) {
                pomData.licenses.each { License license ->
                    licenses += "<p><strong>POM License: $license.name</strong>"
                    if (license.url) {
                        if (license.url.startsWith("http")) {
                            licenses += " - <a href=\"$license.url\">$license.url</a>"
                        } else {
                            licenses += "<p> $license.url</p>"
                        }
                    }
                }
            }
        }

        output << "<td>$name</td>"
        output << "<td>$urls</td>"
        output << "<td>$licenses</td>"

        output << "<td>"
        if (!data.licenseFiles.isEmpty() && !data.licenseFiles.first().files.isEmpty()) {
            output << '<p>'
            output << data.licenseFiles.first().files.collect({ "<a " +
                    "href=\"$libLicenseUrl/$it\">$it</a>" +
                    " " +
                    "" }).join('')
            output << '</p>'
        }
        output << "</td>"
    }
}