buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

version = parent.version

apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

configurations {
    externalLibs {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    testCompile.extendsFrom(externalLibs)
}

dependencies {

    compile project(':jobs-common')

    // CALS API for CALS Jobs
    compile(group: 'gov.ca.cwds.cals', name: 'cals-api', version: calsApiVersion) {
        exclude group: 'org.bouncycastle'
        exclude group: 'org.liquibase', module: 'liquibase-core'
        exclude group: 'gov.ca.cwds.api'
        exclude group: 'org.slf4j'
    }

    // LOGGING:
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4jVersion

    compile group: 'gov.ca.cwds.api', name: 'legacy-data-access-services', version: coreApiVersion

    testCompile 'org.mockito:mockito-core:2.0.54-beta'
//    testCompile group: 'org.liquibase', name: 'liquibase-core', version: '3.5.3'
    testCompile(group: 'gov.ca.cwds.api', name: 'api-core-test-support', version: testSupportVersion) {
        exclude group: 'gov.ca.cwds.api'
    }


    testCompile(group: 'gov.ca.cwds.cals', name: 'lis-data-model', version: cwdsModelVersion, changing: true)
    testCompile(group: 'gov.ca.cwds.cals', name: 'fas-data-model', version: cwdsModelVersion, changing: true)
    testCompile(group: 'gov.ca.cwds.cals', name: 'cws-cms-data-model', version: cwdsModelVersion, changing: true)

    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: jsonAssertVersion
}

shadowJar {
    zip64 true
    classifier = ''
    baseName = (System.properties.getProperty('baseName') != null) ? System.properties.getProperty('baseName') : 'cals-jobs'
    mergeServiceFiles()
    version version + ((System.properties.getProperty('build') != null) ? '.' + System.properties.getProperty('build') : '')
    dependencies { exclude(dependency('org.slf4j:log4j-over-slf4j:1.7.24')) }
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
            groupId 'gov.ca.cwds.jobs'
            artifactId 'cals-jobs-shadow'
        }
    }
    repositories {
        maven {
            url artifactoryRepo
        }
    }
}
